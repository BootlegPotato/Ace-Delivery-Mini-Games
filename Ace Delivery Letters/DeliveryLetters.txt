title Ace Delivery Letters
author Bootleg Potato
homepage www.BootlegPotato.com

background_color  #d5d5d5
text_color #171b1b

========
OBJECTS
========

Background
#d5d5d5

(MailBox
#070909 #696a6a
.....
00000
10001
11011
.....)

MailBox
#171b1b 
.....
.....
..0..
.....
.....

Wall
#696a6a #171b1b
11111
10001
10101
10001
11111

Player
#171b1b #ffffff #696a6a
.222.
.222.
10101
.111.
.....

(Mail
#ffffff #696a6a
.....
11111
01110
00100
.....)

Mail
#ffffff #696a6a
.....
11111
01.10
00100
.....

Pit
#171b1b
.....
.000.
.000.
.000.
.....

Block
#696a6a
.....
.000.
.000.
.000.
.....

PitBlock
#171b1b #d5d5d5
.....
.000.
.010.
.000.
.....

BPit
#171b1b

BBlock
#696a6a

=======
LEGEND
=======

(Terrain Tiles)
. = Background
# = Wall

p = Player

(Goal Tiles)
m = Mail
w = MailBox
% = Mail and MailBox

(Blocks Tiles)
o = Block
x = Pit
n = PitBlock

(Pit Tiles)
y = BPit
q = BBlock


(Fillings Pits)
PitFiller = Block
Filling = PitBlock

(Filling Big Pits)
BPitFiller = BBlock
BFilling = Wall (Yes a big pit becomes a wall)

=======
SOUNDS
=======

startgame 64930302

(Moving Object Sounds)
Mail MOVE 96086307
Block MOVE 36772507
BBlock MOVE 79099907

(Filling Pit Sound)
sfx0 86583504

(Filling BPit Sound)
sfx1 76146702

endlevel 63636303 

endgame 74955903

================
COLLISIONLAYERS
================

Background
Pit, BPit 
PitBlock
MailBox
Player, Wall, Mail, Block, BBlock

======
RULES
======

(Player Push Mail)
[ >  Player | Mail ] -> [  >  Player | > Mail  ]

(Player Push Block)
[ >  Player | Block ] -> [  >  Player | > Block  ]

(Player Push BBlock)
[ >  Player | BBlock ] -> [  >  Player | > BBlock  ]

(don't move into Pit)
[> Player|Pit no Filling] -> [Player|Pit] (Don't move Player)
[> Mail|Pit no Filling] -> [Mail|Pit] (Don't move Mail)

(don't move into BPit)
[> Player|BPit no BFilling] -> [Player|BPit] (Don't move Player)
[> Mail|BPit no BFilling] -> [Mail|BPit] (Don't move Mail)


(Don't move Blocks on Mailboxes)
[> Block|Mailbox] -> [Block|Mailbox] 

(if a Block is on top of a Pit turn it into a PitBlock)
late [Pit no Filling PitFiller] -> [Pit PitBlock] sfx0

(if a BBlock is on top of a BPit turn it into a Wall)
late [BPit no BFilling BPitFiller] -> [BPit Wall] sfx1

==============
WINCONDITIONS
==============

All Mail on MailBox

=======
LEVELS
=======

#####
#...#
#.p.#
#mwm#
#wmw#
#...#
#...#
#####

#######
#..wp.#
#.#w#.#
#...m.#
#wmm.##
#..####
######.







